buildscript {
    ext {
        kotlin_version = '1.9.10'
        typescript_gen_version = '2.19.577'
        guava_version = '31.1-jre'
        mybatis_plus_version = "3.5.4"
        global_version = '1.2.0'
        aliyun_repository = 'https://maven.aliyun.com/nexus/content/groups/public/'
        kotlin_jackson_module_version = '2.13.3'
        swagger_version = '1.7.0'
        springboot_version = '3.1.5'
    }
    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/nexus/content/groups/public" }
        maven { url "https://maven.aliyun.com/nexus/content/repositories/gradle-plugin" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springboot_version")
        classpath("cz.habarta.typescript-generator:typescript-generator-gradle-plugin:$typescript_gen_version")
        classpath "cz.habarta.typescript-generator:typescript-generator-spring:$typescript_gen_version"
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlin_version")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1"
    }
}
plugins {
    id("org.springframework.boot") version "$springboot_version"
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java-library'
    id 'maven-publish'
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'


    group = 'com.uliian'
    version = global_version
    sourceCompatibility = JavaVersion.VERSION_17

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_17
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_17
    }

    repositories {
        mavenLocal()
        maven { url aliyun_repository }
        mavenCentral()
    }

    dependencies {
        testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit5', version: kotlin_version
    }



}

subprojects {
    if(!project.name.contains("demo")) {
        apply from: "${project.rootDir}${File.separator}upload.gradle"
    }
    dependencies {
        api('com.uliian:uliian-commons:1.5.9')
//        api "com.google.guava:guava:$guava_version"
    }

    apply plugin: 'org.springframework.boot'

}


